# DO NOT RENAME THIS FILE
# unless you enjoy infinite loops

export DOTFILES="$HOME/.dotfiles"

# The prompt must be loaded first
source $DOTFILES/zsh/prompt.zsh

# declare an array of our .zsh files
# -U removes duplicate values
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the exports files
for file in ${(M)config_files:#*/exports.zsh}; do
  source "$file"
done

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load antibody plugins
source ~/.zsh_plugins.sh

# load everything but the path, exports, and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh:#*/exports.zsh}; do
  source "$file"
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# top secret
if [[ -a $HOME/.localrc ]]
then
  source $HOME/.localrc
fi

# Fixes wonky control flow issues
stty -ixon -ixoff

# fzf fuzzy finder
# this goes last, I think
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
